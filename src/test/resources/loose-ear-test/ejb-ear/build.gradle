apply plugin: 'ear'
apply plugin: 'liberty'

description = 'EAR Module'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
        }
    }
    dependencies {
        classpath "io.openliberty.tools:liberty-gradle-plugin:$lgpVersion"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    deploy project(':ejb-ejb')
    deploy project(path:':ejb-war', configuration:'archives')
    deploy 'org.apache.commons:commons-text:1.13.1'
    constraints {
        // Override the transitive dependency to address CVE-2025-48924
        implementation('org.apache.commons:commons-lang3:3.18.0') {
            because 'CVE in lower versions'
        }
    }    
    testImplementation group: 'commons-httpclient', name: 'commons-httpclient', version:'3.1'
    testImplementation group: 'junit', name: 'junit', version:'4.13.1'
    libertyRuntime group: runtimeGroup, name: kernelArtifactId, version: runtimeVersion
}

ear {
    archiveFileName = getArchiveBaseName().get() + '.' + getArchiveExtension().get()
    deploymentDescriptor {
        module ('ejb-ejb.jar', 'ejb')
        webModule ('ejb-war.war', '/ejb-war')
    }
}

liberty {
    server {
        name = "ejbServer"
        deploy {
            apps = [ear]
            copyLibsDirectory = file("${project.getLayout().getBuildDirectory().getAsFile().get()}/libs")
        }
        verifyAppStartTimeout = 30
        looseApplication = true
    }
}

deploy.dependsOn 'ear'
ear.dependsOn ':ejb-war:jar', ':ejb-war:war'
