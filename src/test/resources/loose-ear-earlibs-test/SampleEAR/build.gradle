apply plugin: 'ear'
apply plugin: 'liberty'

description = 'EAR Module'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
        }
    }
    dependencies {
        classpath "io.openliberty.tools:liberty-gradle-plugin:$lgpVersion"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    deploy project(':SampleEJB')
    earlib project(path:':SampleWAR', configuration:'warOnly')
    earlib project(path:':SampleWAR2', configuration:'warOnly')
    testImplementation group: 'commons-httpclient', name: 'commons-httpclient', version:'3.1'
    testImplementation group: 'junit', name: 'junit', version:'4.13.1'
    libertyRuntime group: runtimeGroup, name: kernelArtifactId, version: runtimeVersion
}

ear {
    archiveFileName = getArchiveBaseName().get() + '.' + getArchiveExtension().get()
    deploymentDescriptor {
        module ('SampleEJB.jar', 'ejb')
        webModule ('SampleWAR.war', '/SampleWAR')
        webModule ('SampleWAR2.war', '/SampleWAR2')
    }
}

liberty {
    server {
        name = "ejbServer"
        deploy {
            apps = [ear]
        }
        verifyAppStartTimeout = 30
        looseApplication = true
    }
}

deploy.dependsOn 'ear'
ear.dependsOn ':SampleEJB:jar', ':SampleWAR:war', ':SampleWAR2:war'
