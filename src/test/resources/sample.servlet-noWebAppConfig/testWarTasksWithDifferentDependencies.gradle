group = 'liberty.gradle'
version = '1'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath "io.openliberty.tools:liberty-gradle-plugin:$lgpVersion"
    }
}

apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'liberty'

sourceCompatibility = 1.7
targetCompatibility = 1.7

compileJava.options.encoding = 'UTF-8'

ext {
    // Liberty server properties
    wlpServerName = 'LibertyProjectServer'
    serverDirectory = "${project.buildDir}/wlp/usr/servers/${wlpServerName}"
    testServerHttpPort = 9080
    testServerHttpsPort = 9443

    // This is set in the ibm-web-ext.xml file
    warContext = 'sample.servlet-1'
    appName = 'LibertyProject'
    packageDir = "${project.buildDir}"
    packagingType = 'usr'

}
configurations {
  war2.extendsFrom runtime
  war4.extendsFrom runtime
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.cxf:cxf-rt-rs-client:3.1.1'
    testCompile 'org.glassfish:javax.json:1.0.4'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    war2 'org.apache.commons:commons-proxy:1.0'
    war4 'org.apache.commons:commons-text:1.1'
    libertyRuntime group: runtimeGroup, name: runtimeArtifactId, version: runtimeVersion
}

task war2(type:War){
    baseName = 'sample.servlet2'
    classpath = configurations.war2
    println 'war2 archiveName = ' + archiveName
}

task war3(type:War){
    baseName = 'sample.servlet3'
    println 'war3 archiveName = ' + archiveName
}

task war4(type:War){
    baseName = 'sample.servlet4'
    classpath = configurations.war4
    println 'war4 archiveName = ' + archiveName
}

liberty {
    server{
        name = wlpServerName
        serverXmlFile = file("src/main/liberty/config/server-list-test.xml")
        packageLiberty {
            packageDirectory = packageDir
            packageName = appName
            include = packagingType
        }
        looseApplication = false
        deploy {
            apps = [war2, war3]
            dropins = [war, war4]
        }
        verifyAppStartTimeout = 30
    }
}

test {
    println 'inside the test block'
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
    exclude '**/it/**'
}


task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-results/it")
    include '**/it/**'
    exclude '**/unit/**'

    systemProperties = ['liberty.test.port': testServerHttpPort, 'war.name': warContext]
}

task printMessageAboutRunningServer {
    doLast {
        println "The server is now running at http://localhost:${testServerHttpPort}/${warContext}"
        println "To stop the server run 'gradle libertyStop'"
    }
}

check.dependsOn 'integrationTest'
deploy.dependsOn tasks.withType(War)
integrationTest.dependsOn 'libertyStart', 'testClasses'
integrationTest.finalizedBy 'libertyStop'
libertyStart.finalizedBy 'printMessageAboutRunningServer'
