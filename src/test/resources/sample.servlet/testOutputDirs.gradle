group = 'liberty.gradle'
version = '1'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
        }
    }
    dependencies {
        classpath "io.openliberty.tools:liberty-gradle-plugin:$lgpVersion"
    }
}

apply plugin: 'war'
apply plugin: 'liberty'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

ext {
    // Liberty server properties
    wlpServerName = 'LibertyProjectServer'
    serverDirectory = "${project.getLayout().getBuildDirectory().getAsFile().get()}/wlp/usr/servers/${wlpServerName}"
    testServerHttpPort = 9080
    testServerHttpsPort = 9443

    warContext = 'sample.servlet-1'
    appName = 'LibertyProject'
    packageDir = "${project.getLayout().getBuildDirectory().getAsFile().get()}"
    packagingType = 'usr'
}

task testWar(type:War){
    archiveBaseName = 'testWar'
}

task createServerEnv{
    def serverEnvFile = file("src/main/liberty/config/testOutputDir-server.env")
    if (!serverEnvFile.exists()) {
        serverEnvFile.write("WLP_OUTPUT_DIR=${project.getLayout().getBuildDirectory().getAsFile().get()}/testOutputDir")
    }
}

liberty {
    server{
        name = wlpServerName

        serverXmlFile = file("src/main/liberty/config/server-dropins.xml")

        packageLiberty {
            packageDirectory = packageDir
            packageName = appName
            include = packagingType
        }

        serverEnvFile = file("src/main/liberty/config/testOutputDir-server.env")
        looseApplication = false
        deploy {
            apps = [file('build/libs/testWar-1.war')]
        }

        verifyAppStartTimeout = 30
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.apache.cxf:cxf-rt-rs-client:3.1.1'
    testImplementation 'org.glassfish:javax.json:1.0.4'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    libertyRuntime group: runtimeGroup, name: kernelArtifactId, version: runtimeVersion
}

test {
    println 'inside the test block'
    reports.html.outputLocation = file("${project.getLayout().getBuildDirectory().getAsFile().get()}/reports/unit")
    reports.junitXml.outputLocation = file("${project.getLayout().getBuildDirectory().getAsFile().get()}/test-results/unit")
    exclude '**/it/**'
}


task integrationTest(type: Test) {
    group = 'Verification'
    description = 'Runs the integration tests.'
    reports.html.outputLocation = file("${project.getLayout().getBuildDirectory().getAsFile().get()}/reports/it")
    reports.junitXml.outputLocation = file("${project.getLayout().getBuildDirectory().getAsFile().get()}/test-results/it")
    include '**/it/**'
    exclude '**/unit/**'

    systemProperties = ['liberty.test.port': testServerHttpPort, 'war.name': warContext]
}

task printMessageAboutRunningServer {
    doLast {
        println "The server is now running at http://localhost:${testServerHttpPort}/${warContext}"
        println "To stop the server run 'gradle libertyStop'"
    }
}

check.dependsOn 'integrationTest'
libertyCreate.dependsOn 'createServerEnv'
integrationTest.dependsOn 'libertyStart', 'testClasses'
integrationTest.finalizedBy 'libertyStop'
libertyStart.finalizedBy 'printMessageAboutRunningServer'
