group = 'liberty.gradle'
version = '1'

apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'liberty'

sourceCompatibility = 1.7
targetCompatibility = 1.7

compileJava.options.encoding = 'UTF-8'

ext {
    libertyVersion = project.property("libertyVersion")
    // Liberty server properties
    wlpServerName = 'LibertyProjectServer'
    serverDirectory = "${project.buildDir}/wlp/usr/servers/${wlpServerName}"
    testServerHttpPort = 9080
    testServerHttpsPort = 9443

    // This is set in the ibm-web-ext.xml file
    warContext = 'sample.servlet-1'
    appName = 'LibertyProject'
    packageFile = "${project.buildDir}/${appName}.zip"
    packagingType = 'usr'

}

liberty {
    install {
        runtimeUrl = "http://repo1.maven.org/maven2/com/ibm/websphere/appserver/runtime/wlp-webProfile7/${libertyVersion}/wlp-webProfile7-${libertyVersion}.zip"
    }
    server{
        configFile = file("src/main/liberty/config/server-apps-test.xml")

        name = wlpServerName
        looseApplication = false

        packageLiberty {
            archive = packageFile
            include = packagingType
        }

        verifyAppStartTimeout = 30
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.cxf:cxf-rt-rs-client:3.1.1'
    testCompile 'org.glassfish:javax.json:1.0.4'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
}

task ('runGradleTest') {
    dependsOn 'clean', 'libertyStart'
    finalizedBy 'libertyStop'

    doLast {
        assert new File("${buildDir}/wlp/usr/servers/${wlpServerName}/apps/sample.servlet-1.war").exists() : 'application not installed on server'
    }
}

installApps.dependsOn 'war'
