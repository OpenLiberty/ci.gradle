import java.io.FileOutputStream
import java.io.IOException
import java.io.OutputStream
import java.util.Properties

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "com.gradle.plugin-publish"

archivesBaseName = 'liberty-gradle-plugin'
group = 'io.openliberty.tools'
version = '3.1.2-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://public.dhe.ibm.com/ibmdl/export/pub/software/websphere/wasdev/maven/repository/'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.12.0"
    }
}

configurations {
    provided
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

compileGroovy {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

compileTestGroovy {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

def libertyAntVersion = "1.9.7"
def libertyCommonVersion = "1.8.12"

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation ("io.openliberty.tools:liberty-ant-tasks:$libertyAntVersion")
    implementation ("io.openliberty.tools:ci.common:$libertyCommonVersion")
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'
    provided group: 'com.ibm.websphere.appserver.spi', name: 'com.ibm.websphere.appserver.spi.kernel.embeddable', version: '1.0.0'
    testImplementation 'junit:junit:4.11'
    testImplementation gradleTestKit()
}

sourceSets.main.compileClasspath += configurations.provided

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar) {
    classifier = 'groovydoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourcesJar
}

test {
    minHeapSize = "1G"
    maxHeapSize = "3G"
    jvmArgs '-Xmx3G'

    systemProperties System.getProperties()
    systemProperty 'runit', 'online'

    doFirst {
        String runtimeGroup
        String runtimeArtifactId
        String kernelArtifactId
        String libertyRuntime = System.getProperty('runtime')
        String runtimeVersion = System.getProperty('runtimeVersion')

        if (libertyRuntime == null || libertyRuntime.isEmpty()) {
            throw new GradleException('Tests could not be run. Please specify a Liberty runtime. Choose either wlp or ol.')
        }
        if (runtimeVersion == null || runtimeVersion.isEmpty()) {
            throw new GradleException('Tests could not be run. Please specify a Liberty runtime version.')
        }

        Properties prop = new Properties()
        OutputStream output = null

        try {

        output = new FileOutputStream("${buildDir}/gradle.properties");

        if (libertyRuntime == "ol") {
            runtimeGroup = "io.openliberty"
            runtimeArtifactId = "openliberty-runtime"
            kernelArtifactId = "openliberty-kernel"
        } else {
            runtimeGroup = "com.ibm.websphere.appserver.runtime"
            runtimeArtifactId = "wlp-javaee7"
            kernelArtifactId = "wlp-kernel"
        }

        // set the properties value
        prop.setProperty("lgpVersion", version)
        prop.setProperty("runtimeGroup", runtimeGroup)
        prop.setProperty("runtimeArtifactId", runtimeArtifactId)
        prop.setProperty("kernelArtifactId", kernelArtifactId)
        prop.setProperty("runtimeVersion", runtimeVersion)
        prop.setProperty("antVersion", libertyAntVersion)
        prop.setProperty("commonVersion", libertyCommonVersion)

        // save properties to project root folder
        prop.store(output, null)

        } catch (IOException io) {
            io.printStackTrace()
        } finally {
            if (output != null) {
                try {
                    output.close()
                } catch (IOException e) {
                    e.printStackTrace()
                }
            }
        }
    }
    if (project.hasProperty('test.exclude')){
        for (String pattern: project.property('test.exclude').split(',')) {
            exclude pattern
        }
    }
    if (project.hasProperty('test.include')){
        for (String pattern: project.property('test.include').split(',')) {
            include pattern
        }
    }
}

pluginBundle {
    website = 'https://github.com/OpenLiberty/ci.gradle'
    vcsUrl = 'https://github.com/OpenLiberty/ci.gradle'
    description = 'Gradle plugin for managing Liberty servers'
    tags = ['liberty', 'websphere', 'devops']

    plugins {
        cigradle {
            id = 'io.openliberty.tools.gradle.Liberty'
            displayName = 'ci.gradle'
        }
    }
}

publishing {
    publications {
        libertyGradle (MavenPublication) {
            artifactId = archivesBaseName

            from components.java
            artifact groovydocJar
            artifact sourcesJar 

            pom {
                name = 'liberty-gradle-plugin'
                packaging = 'jar'
                description = 'Liberty Gradle Plug-in.'
                url = 'https://wasdev.github.io'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://raw.github.com/OpenLiberty/ci.gradle/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'jgawor'
                        name = 'Jarek Gawor'
                        email = 'jgawor@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:OpenLiberty/ci.gradle.git'
                    developerConnection = 'scm:git:git@github.com:OpenLiberty/ci.gradle.git'
                    url = 'git@github.com:OpenLiberty/ci.gradle.git'
                    tag = 'HEAD'
                }
            }
        }
    }
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    
    if (!version.endsWith("SNAPSHOT")) {
        signing {
            sign publishing.publications.libertyGradle
        }
    } else {
        signing.required = false
    }

    publishing {
        repositories {
            maven {
                name = "Nexus"
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}

task install {
    dependsOn "publishToMavenLocal", ":pluginRepo:publishTestPublicationToTestRepository"
}

publishToMavenLocal.dependsOn jar

groovydoc.enabled = false